apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

android {
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file(getRootDir().absolutePath + File.separator + "debug.keystore")
            storePassword 'android'
        }
    }
    compileSdkVersion rootProject.ext.android.compileSdkversions
    buildToolsVersion rootProject.ext.android.buildToolsversions

    defaultConfig {
        applicationId "org.smile.mde"
        minSdkVersion rootProject.ext.android.minSdkversions
        targetSdkVersion rootProject.ext.android.targetSdkversions
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        ndk {
            //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）"arm64-v8a", "x86","x86_64"
            abiFilters "armeabi", "armeabi-v7a"
        }

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    // Butterknife requires Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '28.0.0'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation project(':commonlib')
    implementation project(':searchfunctionlib')
    implementation project(':cropperLibrary')

    // 引入butterknife
    annotationProcessor(rootProject.ext.dependencies["butterknife-compiler"]) {
        exclude group: 'support-annotations'
        exclude group: 'support-v7'
        exclude group: 'support-v4'
    }
    // 引入arouter
    annotationProcessor('com.alibaba:arouter-compiler:1.2.2') {
        exclude group: 'support-annotations'
        exclude group: 'support-v7'
        exclude group: 'support-v4'
    }
    //引入dagger2和dagger2-android
    annotationProcessor rootProject.ext.dependencies["dagger2-compiler"]
    annotationProcessor rootProject.ext.dependencies["dagger2-android-processor"]
}
